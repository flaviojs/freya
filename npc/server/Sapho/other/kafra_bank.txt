//===== Freya Script =========================================
//= The 2nd Bank of Prontera (with daily 0.01% income!)
//===== By: ==================================================
//= Lupus (1.0)
//= Yor (1.3/1.4)
//= Sensei (1.5)
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= Freya (any)
//===== Description: =========================================
//= A bank which has an interest %
//===== Additional Comments: =================================
// Look for this formula and setup your Bank daily % interest
// #kafrabank/1000 = 0.1% of interest per day (default)
// #kafrabank/100  =  1%  of interest per day
// #kafrabank/10   =  10% of interest per day
//
// 1.1 Added log of bank operation -> logmes "some info";
// 1.2 Set max income to 100,000z. It would help to avoid
//     zeny exploits when you change DATE at your server
// 1.3 Fix logmes, limit bank balance to 1,000,000,000z [Yor]
// 1.4 Fix interest reg value [Yor]
// 1.5 Added bank in all cities [Sensei]
// 1.6 French translation [Sensei]
//============================================================

prontera.gat,131,190,1	script	Banquier::bankg	833,{
	mes"[Banquier]";
	mes strcharinfo(0) + ", bienvenue à la premiere banque midgardienne!";

	set @kb_int,(gettime(6) * 31) + gettime(5); // today's number
	set @income,0;
	// calculate %
	if (#kafrabank < 0) set #kafrabank,0;
	if (#kafrabank == 0) goto L_NoIncomeToday;
	if (#kb_int > @kb_int && #kb_int < @kb_int + 372) set #kb_int,#kb_int - 372; // fix new year (372 = 12 * 31)
	if (#kb_int >= @kb_int) goto L_NoIncomeToday;
	set @income,(#kafrabank / 1000) * (@kb_int - #kb_int); // @income == % of the sum
	// max income constant:
	if (@income > 100000) set @income,100000;

L_NoIncomeToday:
	set #kb_int,@kb_int; // reset days timer

	if (#kafrabank == 0) mes "Nous pouvons vous ouvrir un compte.";
	if (#kafrabank > 0) mes "Votre compte est créditeur de: ^135445" + #kafrabank + "^000000 zeny.";
	if (@income > 0) mes "Vos intérêts quotidiens vous ont rapporté: ^135445" + @income + "^000000 zeny.";
	set #kafrabank,#kafrabank + @income;
	if (@income > 0) mes "Votre nouveau solde est maintenant de: ^135445" + #kafrabank + "^000000 zeny.";
	mes "Que voulez-vous?";
	next;
	if (#kafrabank == 0) menu "-Ouvrir un compte",-,"-Rien, au revoir.",B_EXIT2;
	if (#kafrabank > 0) menu "-Faire un dépot",-,"-Faire un retrait",M_WITHDRAW,"-Rien, au revoir",B_EXIT2;

	mes"[Banquier]";
	mes "Combien voulez-vous déposer? (min.: 1000z).";
	next;
	input @kafrabank;

	if (@kafrabank < 1) goto B_EXIT2;
	if (@kafrabank < 1000) goto L_LESS_1000;
	if (@kafrabank > 10000000) goto L_TOO_BIG_DEPOSIT;
	if (@kafrabank > zeny) goto L_NOT_ENOUGH;
	if (@kafrabank + zeny > 1000000000) goto L_TOO_BIG_BALANCE;
	set zeny,zeny - @kafrabank;
	set #kafrabank, #kafrabank + @kafrabank;
	mes"[Banquier]";
	mes "Vous venez de déposer ^135445" + @kafrabank + "z^000000.";
	// we log these zeny operations into the log db
	if (@income > 0) logmes "Balance: " + (#kafrabank - @kafrabank) + "z, Income: " + @income +"z, Deposit: "+ @kafrabank +"z, Final: "+ #kafrabank +"z";
	if (@income <= 0) logmes "Balance: " + (#kafrabank - @kafrabank) + "z, Deposit: " + @kafrabank + "z, Final: " + #kafrabank + "z";
	goto B_EXIT;

L_LESS_1000:
		mes"[Banquier]";
		mes "Désolé, mais vous devez déposer au moins 1.000 zeny.";
		next;
		goto B_EXIT2;

L_TOO_BIG_DEPOSIT:
		mes"[Banquier]";
		mes "Désolé, mais vous ne pouvez pas déposer plus de 10.000.000 zeny.";
		next;
		goto B_EXIT2;

L_TOO_BIG_BALANCE:
		mes"[Banquier]";
		mes "Désolé, mais vous ne pouvez pas avoir plus de 1.000.000.000 zeny sur votre compte.";
		next;
		goto B_EXIT2;

M_WITHDRAW:
	mes"[Banquier]";
	mes "Solde de votre compte: ^135445" + #kafrabank + "^000000 zeny.";
	mes "Combien voulez-vous retirer?";
	next;
	input @kafrabank;

	if (@kafrabank < 1) goto B_EXIT2;
	if (@kafrabank > 10000000) goto L_TOO_BIG_WITHDRAW;
	if (@kafrabank > #kafrabank) goto L_NOT_ENOUGH;
	set #kafrabank,#kafrabank - @kafrabank;
	set zeny,zeny + @kafrabank;
	mes"[Banquier]";
	mes "Voici vos ^135445" + @kafrabank + "z^000000, signez ici...";
	// we log these zeny operations into the log db
	if (@income > 0) logmes "Balance: " + (#kafrabank + @kafrabank) + "z, Income: " + @income +"z, Withdraw: "+ @kafrabank +"z, Final: "+ #kafrabank +"z";
	if (@income <= 0) logmes "Balance: " + (#kafrabank + @kafrabank) + "z, Withdraw: " + @kafrabank + "z, Final: " + #kafrabank + "z";
	goto B_EXIT;

L_NOT_ENOUGH:
	mes"[Banquier]";
	mes "Vous n'avez pas assez de zeny pour cette opération.";
	next;
	goto B_EXIT2;

L_TOO_BIG_WITHDRAW:
	mes"[Banquier]";
	mes "Désolé, mais vous ne pouvez retirer au maximum que 10.000.000 zeny à la fois.";
	next;
	goto B_EXIT2;

B_EXIT:
	mes "Votre nouveau solde: ^135445" + #kafrabank + "z^000000.";
	mes "Très bien... Revenez quand vous voulez!";
	next;

B_EXIT2:
	if (#kafrabank == 0) set #kb_int,0; // reset account_reg #kb_int value
	mes"[Banquier]";
	mes "Merci pour la confiance que vous accordez à nos services. A la prochainne fois.";
	close;
}

geffen.gat,125,73,3	duplicate(bankg)	Banquier#2	833
izlude.gat,145,107,1	duplicate(bankg)	Banquier#3	833
morocc.gat,147,84,7	duplicate(bankg)	Banquier#4	833
payon.gat,170,225,5	duplicate(bankg)	Banquier#5	833
alberta.gat,25,238,5	duplicate(bankg)	Banquier#6	833
aldebaran.gat,135,123,4	duplicate(bankg)	Banquier#7	833
xmas.gat,154,126,9	duplicate(bankg)	Banquier#8	833
comodo.gat,195,151,5	duplicate(bankg)	Banquier#9	833
yuno.gat,339,109,6	duplicate(bankg)	Banquier#10	833
amatsu.gat,102,152,3	duplicate(bankg)	Banquier#11	833
gonryun.gat,157,122,2	duplicate(bankg)	Banquier#12	833
umbala.gat,130,133,6	duplicate(bankg)	Banquier#13	833
louyang.gat,210,102,3	duplicate(bankg)	Banquier#14	833
pay_arche.gat,55,121,3	duplicate(bankg)	Banquier#15	833
ayothaya.gat,212,180,3	duplicate(bankg)	Banquier#16	833
einbroch.gat,57,203,4	duplicate(bankg)	Banquier#17	833
lighthalzen.gat,154,105,3	duplicate(bankg)	Banquier#18	833
einbech.gat,181,126,2	duplicate(bankg)	Banquier#19	833
hugel.gat,103,143,7	duplicate(bankg)	Banquier#20	833