//===== Script Athena =================================================//
//= Script 1-Partie_de_Yuno.txt                                        //
//===== Par: ==========================================================//
//= Kylvan                                                             //
//= Kylvan@laposte.net                                                 //
//= Although all this quest is in French, you can mail me in English,  //
//= would be able to answer you.                                       //
//===== Version Actuelle: =============================================//
//= 1.0                                                                //
//===== Compatible Avec: ==============================================//
//= Freya 2.0.2 and newer.                                             //
//===== Description: ==================================================//
//= Il s'agit de la premiere partie de la quete permettant de renaitre.//
//= Le joueur devra repondre a un questionnaire, puis parler a         //
//= differents NPC.                                                    //
//= Si le joueur est un high novice alors il le téléporte a Valkyrie.  //
//===== Commentaires: =================================================//
// Pour renaitre il faut etre lv 99, etre job level 50 et              //
// être n'importe quelle classe 2, super-novice inclus.                //
//=====================================================================//


// -== Researcher of the Book of Ymir ==-
yuno_in02.gat,90,77,4	script	Chercheur	744,{
	mes "[Chercheur]";
	mes "Mais où peut-il être ?!";
	mes "Il DOIT être pres d'ici !";
	mes "Je vais fouiller plus profondément dans le château.";
	close;
}

// -== The Librarian that watches the "Book of Ymir" ==-
yuno_in02.gat,91,176,5	script	Bibliothecaire	754,{

	if (readparam(19) == 4001) goto L_warpnovice;
	if (readparam(19) > 4001) goto L_nowarp;
	if (Q_valkyrie > 0) goto L_Done;
	if ((readparam(19) >= 7)&&(readparam(11)>=95)&&(readparam(55)>=50)) goto L_Ok;
	mes "[Bibliothecaire]";
	mes "Regardez si vous le désirez, mais ne touchez surtout pas au livre ! Seuls quelques initiés peuvent s'en approcher.";
	close;

L_nowarp:
	mes "[Bibliothecaire]";
	mes "Alors que vous vivez une seconde jeunesse, je sombre dans les affres de la vieillesse...";
	next;
	mes "[Bibliothecaire]";
	mes "Je suis désolé mais je n'ai plus la force d'invoquer un portail pour le Sanctuaire. Peut-etre est-ce là une punition divine pour vous avoir envié..";
	next;
	mes "[Bibliothecaire]";
	mes "Vous allez devoir trouver quelqu'un d'autre pour vous emmener au Sanctuaire... Navré.";
	close;

L_warpnovice:
	mes "[Bibliothecaire]";
	mes "Haha, je suis heureux de voir que je n'ai pas ouvert le Livre à n'importe qui !";
	mes "Le don des Dieux... Ca doit etre quelque chose hein ?";
	next;
	mes "[Bibliothecaire]";
	mes "Moi il ne me l'ont jamais accordé... Juste placé ici a surveiller le Livre.";
	next;
	mes "[Bibliothecaire]";
	mes "Mais les servir est deja une recompense en soit !";
	next;
	mes "[Bibliothecaire]";
	mes "Voulez-vous que j'ouvre un portail qui vous menera au Sanctuaire ?";
	menu "Oui",L_oui,"Non",-;
	next;
	mes "[Bibliothecaire]";
	mes "Comme vous voulez.";
	close;

L_oui:
	// mettre tous les flag qu'il faut ;p
	setmapflagnosave "valkyrie.gat","yggdrasil01.gat",0,0;
	setmapflag "valkyrie.gat",0; // no memo
	//setmapflag "valkyrie.gat",2; // no save
	setmapflag "valkyrie.gat",13; // no warp
	warp "valkyrie.gat",48,9;
	end;

L_Ok:
	mes "[Bibliothecaire]";
	mes "Moi, le 78ème Bibliothecaire, Gardien des ordres secrets, ai juré de proteger ce Livre.";
	next;
	mes "[Bibliothecaire]";
	mes "Pour que je vous explique comment lire ce Livre, il faut que je sache quel type de personne vous êtes.";
	mes "Seuls de sages erudits doivent pouvoir pretendre à ce savoir.";
	next;
	mes "[Bibliothecaire]";
	mes "Acceptez-vous de passer ce test ?";
	menu "Oui",test,"Non",-;
	next;
	mes "[Bibliothecaire]";
	mes "Au revoir.";
	close;

test:
	next;
	set @point,0;
	mes "[Bibliothecaire]";
	mes "Dans l'Armurerie de Yuno, quel equipement est fierement exposé au milieu de la piece ?";
	menu "Une épée",-,"Une hache",faux1,"Un bouclier",faux1,"un casque",faux1;
	set @point,@point+1;

faux1:
	next;
	mes "[Bibliothecaire]";
	mes "Combien d'îlots flottants trouve-t-on autour de l'île principale de Gon Ryun ?";
	menu "7",-,"6",faux2,"5",faux2,"4",faux2;
	set @point,@point+1;

faux2:
	next;
	mes "[Bibliothecaire]";
	mes "A partir d'Alberta, laquelle de ces destinations n'est pas déservie par la Kafra ?";
	menu "Comodo",faux3,"Payon",faux3,"Geffen",-,"Prontera",faux3;
	set @point,@point+1;

faux3:
	next;
	mes "[Bibliothecaire]";
	mes "Lequel de ces personnages n'est pas un Employé de chateau à Payon ?";
	menu "Garriet",faux4,"Nadjarf",-,"Grunday",faux4,"Gordhan",faux4;
	set @point,@point+1;

faux4:
	next;
	mes "[Bibliothecaire]";
	mes "Où peut-on obtenir des Kafra Pass ?";
	menu "Geffen",faux5,"Prontera",faux5,"Al de Baran",-,"Yuno",faux5;
	set @point,@point+1;

faux5:
	next;
	mes "[Bibliothecaire]";
	mes "Quel est la bonne orthographe de cette aptitude permettant d'augmenter sa frappe grace a son argent ?";
	menu "Mamonite",faux6,"Manonite",faux6,"Mannonite",faux6,"Mammonite",-;
	set @point,@point+1;

faux6:
	next;
	mes "[Bibliothecaire]";
	mes "Quel est l'Emblem de la cité de Prontera ?";
	menu "Un lion sur fond Bleu ou jaune",faux7,"Un aigle a deux têtes sur fond Vert ou Jaune",-,"Un Dragon à six pattes sur fond Rouge ou blanc",faux7,"Une Fleur de lys sur fond Bleu ou Jaune",faux7;
	set @point,@point+1;

faux7:
	next;
	mes "[Bibliothecaire]";
	mes "Combien y a-t-il de rangées de Bancs dans l'Ancienne Eglise de Glast Heim ?";
	menu "10",faux8,"9",-,"8",faux8,"7",faux8;
	set @point,@point+1;

faux8:
	next;
	mes "[Bibliothecaire]";
	mes "Laquelle de ces plantes peu faire oublier le passé ?";
	menu "Aloé",faux9,"Ment",-,"Illusion Flower",faux9,"Singing Plant",faux9;
	set @point,@point+1;

faux9:
	next;
	mes "[Bibliothecaire]";
	mes "Parmis ces equipements, un seul nécessite l'utilisation d'un Drifting Air pour sa conception, lequel ?";
	menu "Brisingamen",-,"Sleipnir",faux10,"Megingjard",faux10,"Gleipnir",faux10;
	set @point,@point+1;

faux10:
	next;
	mes "[Bibliothecaire]";
	mes "Pourquoi désirez vous acquerir ces connaissances ?";
	menu "Le Pouvoir",-,"La Notoriété",-,"L'Aventure",-,"La Distration",-;
	next;
	if (@point>7) goto L_Test_succes;
	mes "[Bibliothecaire]";
	mes "Vous n'êtes pas un érudit et votre sagesse est plus que douteuse. Je ne vous laisserais pas acquérir ce savoir.";
	next;
	mes "[Bibliothecaire]";
	mes "Partez.";
	close;

L_Test_succes:
	mes "[Bibliothecaire]";
	mes "Aventurez-vous profondément dans le château de Yuno, dans le coeur de Ymir. Là, vous trouverez le seul être capable de vous permettre de comprendre ces ecrits.";
	next;
	mes "[Bibliothecaire]";
	mes "Allez, puissent les secrets vous proteger.";
	set Q_valkyrie,1;
	close;

L_Done:
	mes "[Bibliothecaire]";
	mes "Je n'ai plus rien à vous apprendre.";
	close;
}


// -== The Heart of Ymir ==-
yuno_in05.gat,80,44,0	script	Coeur de Ymir	803,{
	if (Q_valkyrie < 1) goto not_applicable;
	if (Q_valkyrie == 1) goto L_Listen;
	if (Q_valkyrie == 2) goto L_Done;
	mes "[Coeur de Ymir]";
	mes "Le Livre ne s'ouvrira pas pour vous.";
	close;

L_Listen:
	mes "[Coeur de Ymir]";
	mes "Je lis dans votre coeur que vous avez parcouru le Monde, mené de nombreuses batailles et traversé suffisament d'epreuves,  pour devenir un veritable Sage.";
	mes "Mais cette decision ne m'appartient pas. Je vous permet cependant de lire le Livre de Ymir.";
	mes "Maintenant, allez. Le sceau de l'ignorance est brisé, le Livre vous revelera son savoir.";
	set Q_valkyrie,2;
	close;

not_applicable:
	mes "[Coeur de Ymir]";
	mes "Le Livre ne s'ouvrira pas pour vous.";
	close;

L_Done:
	mes "[Coeur de Ymir]";
	mes "Je n'ai plus rien à vous apprendre.";
	close;
}


// -== Book of Ymir (Heavens Door) ==-
yuno_in02.gat,93,207,0	script	Livre de Ymir	111,{
	if (Q_valkyrie == 1) goto L_Faded;
	if (Q_valkyrie == 2) goto L_Start;
	mes "[Livre de Ymir]";
	mes ". . . . .";
	close;

L_Faded:
	mes "[Livre de Ymir]";
	mes "*Le livre s'ouvre lorsque vous approchez*";
	next;
	mes "[Livre de Ymir]";
	mes "*Vous essayez de lire, mais les mots se derobent. Sitôt les lisez-vous, sitôt disparaissent-ils.*";
	close;

L_Start:
	mes "[Livre de Ymir]";
	mes "*Le livre s'ouvre lorsque vous approchez*";
	next;
	mes "[Livre de Ymir]";
	mes "Au commencement des temps, les Dieux choisir de jeunes gens braves et vifs d'esprit et les formèrent aux Arts délicats de la Sorcellerie et de la Magie.";
	next;
	mes "[Livre de Ymir]";
	mes "La Légion des Sages venait de naître.";
	next;
	mes "[Livre de Ymir]";
	mes "Durant des millénaires, les Sages dispensèrent leur enseignement aux peuples et les maintinrent sous la protection divine. Seuls quelques uns de leurs disciples furent acceptés en leur rang, et la veritable Connaissance leur fut revelée afin qu'ils puissent poursuivre l'oeuvre de leur maîtres.";
	next;
	mes "[Livre de Ymir]";
	mes "Mais un jour, quatre Sages trahirent les leurs.";
	mes "Ils se servirent de leur Connaissance pour invoquer en ce monde des puissances démoniques afin de les contrôler et de reigner en maître. Mais ils ne purent contenir les puissances conjurées, et le monde sombra dans les ténèbres de la guerre.";
	next;
	mes "[Livre de Ymir]";
	mes "Des siecles durant, les terres du monde furent dévastées par les combats et les sortilèges.";
	next;
	mes "[Livre de Ymir]";
	mes "Les enseignements et connaissances des Sages furent dispercés par la guerre. Et les Sages eux-même ne laisserent de traces que dans de rares ecrits. La guerre causa des pertes colossales pour les démons comme pour les humains et une treve fut decidée.";
	next;
	mes "[Livre de Ymir]";
	mes "Les démons se replierent dans leur abysses et tous les ordres religieux humains s’unirent alors pour en sceller a jamais l’entrée. L’energie deployée lors de l’incantation fut telle qu’aucun pretre ne survecu mais leur devoir était accomplit : le sceau garant de la paix était placé.";
	next;
	mes "[Livre de Ymir]";
	mes "Les Sages félons furent jugés par leurs semblables qui les condamnerent a rester seuls dans ce monde qui ne les connaissait plus. Le faible sceau, placé par les pretres dépourvu de la Connaissance des Sages, se brisera et les hordes démoniaques plongeront de nouveau le monde dans le chaos.";
	next;
	mes "[Livre de Ymir]";
	mes "Trouvez les Sages et laissez-les vous guider vers la Connaissance.";
	next;
	mes "[Livre de Ymir]";
	mes "Des ruines d'une cité déchue, un Sage éprouvera votre Esprit";
	next;
	mes "[Livre de Ymir]";
	mes "Du sommet d'une haute tour, un Sage éprouvera votre Force";
	next;
	mes "[Livre de Ymir]";
	mes "Des rives d'un lac de flammes, un Sage éprouvera votre Volonté";
	next;
	mes "[Livre de Ymir]";
	mes "Du jardin de la vie, au-delà du Royaume des Morts, un Sage vous guidera vers la Connaissance.";
	if (sage==0) goto L_p;
	close;

L_p:
	set sage, 1;
	set jour1,gettime(5);
	set mois1,gettime(6);
	set an1,gettime(7);
	set heure1,gettime(3);
	set min1,gettime(2);
	set sec1,gettime(1);
	close;
}
