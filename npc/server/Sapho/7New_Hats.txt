//===== Script Freya ===================================================//
//= New_Hats.txt                                                       =//
//===== Par: Tets-UO ===================================================//
//= a.k.a. Mégami [megami@noos.fr]                                     =//
//= Staff Sapho RO [sapho.neo-avalon.org]                              =//
//= Syntaxe du code par Kylvan (Kylvan@laposte.net)                    =//
//===== Version Actuelle: ==============================================//
//= v.1.0                                                              =//
//===== Compatible Avec: ===============================================//
//= Freya SVN                                                          =//
//===== Description: ===================================================//
//= 7 Nouveaux kRO Hats                                                =//
//===== Commentaires: ==================================================//
//= En attendant les quêtes 'officielles' (?)                          =//
//=                                                                    =//
//======================================================================//


// New hats

lou_in02.gat,210,47,4	script	Le Chapelier Fou	806,{

	mes "[Le Chapelier Fou]";
	mes "Quelle belle journée =)";
	next;
	mes "[Le Chapelier Fou]";
	mes "Je me présente...";
	mes "Je suis Le Chapelier Fou !(:p)";
	next;
	mes "[Le Chapelier Fou]";
	mes "Quel joli chapeau désirez-vous?";
	next;

	setarray @Hats[0],5110,5108,5117,5109,5107,5121,5115;

// 00--> Baby Suckle (Baby Rubber) [5110]
// 01--> Hat of the Investigator (Detective Hat) [5108]
// 02--> Mystic Rose [5117]
// 03--> Red Bonnet [5109]
// 04--> Well-Baked Toast (Well Done Toast) [5107]
// 05--> Mask of Zherlthsh [5121]
// 06--> Wide Bunker Hat (Fashionable Fur Hat) [5115]

	deletearray @item[0],128;
	deletearray @quantite[0],128;

	menu getitemname(@Hats[0]),H_0,getitemname(@Hats[1]),H_1,getitemname(@Hats[2]),H_2,getitemname(@Hats[3]),H_3,getitemname(@Hats[4]),H_4,getitemname(@Hats[5]),H_5,getitemname(@Hats[6]),H_6;

H_0: 
// Baby Suckle
	set @Hat,@Hats[0];
	setarray @item[0],526,7270,10004,941;
	//Magnifier, Baby Bottle, Pacifier, Nose Ring
	setarray @quantite[0],1,1,1,1;
	set @prix,50000;
	// 50.000 Zenys
	goto Desc_item;

H_1: 
// Hat of the Investigator (Detective Hat)
	set @Hat,@Hats[1];
	setarray @item[0],611,7301,5115;
	//Magnifier, Tassel, Wide Bunker Hat (Fashionable Fur Hat)
	setarray @quantite[0],10,1887,1;
	set @prix,50000;
	// 50.000 Zenys
	goto Desc_item;

H_2: 
// Mystic Rose
	set @Hat,@Hats[2];
	setarray @item[0],731,748,982;
	//2-Carat Diamonds, Witherless Roses, White Dyestuff
	setarray @quantite[0],10,3,1;
	set @prix,50000;
	// 50.000 Zenys
	goto Desc_item;

H_3: 
// Red Bonnet
	set @Hat,@Hats[3];
	setarray @item[0],10015,975,10007,5032;
	//Green Lace, Scarlet Dyestuff, Silk Ribbon, Sunday Hat
	setarray @quantite[0],1,1,1,1;
	set @prix,50000;
	// 50.000 Zenys
	goto Desc_item;

H_4: 
// Well-Baked Toast (Well Done Toast) 
	set @Hat,@Hats[4];
	setarray @item[0],519,548,539,7031;
	//Milk, Cheese, Piece of Cake, Old Frying Pan
	setarray @quantite[0],50,50,50,50;
	set @prix,15000;
	// 15.000 Zenys
	goto Desc_item;

H_5: 
// Mask of Zherlthsh 
	set @Hat,@Hats[5];
	setarray @item[0],7205,660,7263,7099;
	//Black Pieces of Cloth, Forbidden Red Candle, Cat's Eye, Worn-out Magic Scrolls
	setarray @quantite[0],369,1,1,30;
	set @prix,50000;
	// 50.000 Zenys
	goto Desc_item;

H_6: 
// Wide Bunker Hat (Fashionable Fur Hat)
	set @Hat,@Hats[6];
	setarray @item[0],7267,983,749;
	//Tigerskin Underwears, Black Dyestuff, Frozen Rose
	setarray @quantite[0],999,1,1;
	set @prix,100000;
	// 100.000 Zenys
	goto Desc_item;

Desc_item:
	next;
	mes "[Le Chapelier Fou]";
	mes "Pour obtenir le "+getitemname(@Hat)+" il me faut les objets suivants :";

	set @i,0;
	set @arraysize,getarraysize(@item);
	Boucle:
		if (@i<@arraysize) mes "- "+@quantite[@i]+" "+getitemname(@item[@i]);
		if (@i<=@arraysize) set @i,@i+1;
		if (@i<=@arraysize) goto Boucle;
	mes "Et bien entendu, "+@prix+" Zenys.";
	next;
	mes "[Le Chapelier Fou]";
	mes "Si vous possedez les objets necessaires, je me ferai un plaisir de vous donnez le hat que vous voulez.";
	menu "Oui",yep,"Non",nop;

nop:
	next;
	mes "[Le Chapelier Fou]";
	mes "Beuh... ~~";
	next;
	mes "[Le Chapelier Fou]";
	mes "Bah à bientôt alors...";
	close;

yep:
	next;
	set @i,0;
	set @arraysize,getarraysize(@item);
	Check_item:
		if (@i>=@arraysize) goto L_Check_Success;
		if (countitem(@item[@i])<@quantite[@i]) goto L_Check_Failure;
		set @i,@i+1;
		goto Check_item;

L_Check_Failure:
	mes "[Le Chapelier Fou]";
	mes "Désolé mais vous ne disposez pas de tous les objets ou Zenys necessaires.";
	close;

L_Check_Success:
	if (Zeny < @prix) goto L_Check_Failure;
	set @i,0;
	set @arraysize,getarraysize(@item);
	Del_item:
		if (@i<@arraysize) delitem @item[@i],@quantite[@i];
		if (@i<=@arraysize) set @i,@i+1;
		if (@i<=@arraysize) goto Del_item;
	set Zeny,Zeny-@prix;
	getitem @Hat,1;
	mes "[Le Chapelier Fou]";
	mes "Tadaaa !";
	mes "Voici votre hat =)";
	close;
}