//===== Freya Script =========================================
//= The 2nd Bank of Prontera (with daily 0.01% income!)
//===== By: ==================================================
//= Lupus (1.0)
//= Yor (1.3/1.4)
//= Sensei (1.5)
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: =====================================
//= Freya (any)
//===== Description: =========================================
//= A bank which has an interest %
//===== Additional Comments: =================================
// Look for this formula and setup your Bank daily % interest
// #kafrabank/1000 = 0.1% of interest per day (default)
// #kafrabank/100  =  1%  of interest per day
// #kafrabank/10   =  10% of interest per day
//
// 1.1 Added log of bank operation -> logmes "some info";
// 1.2 Set max income to 100,000z. It would help to avoid
//     zeny exploits when you change DATE at your server
// 1.3 Fix logmes, limit bank balance to 1,000,000,000z [Yor]
// 1.4 Fix interest reg value [Yor]
// 1.5 Added bank in all cities [Sensei]
//============================================================

prontera.gat,131,190,1	script	Bank Clerk::bankg	833,{
	mes"[Maniss]";
	mes strcharinfo(0) + ", welcome to the 2nd Bank of Prontera!";

	set @kb_int,(gettime(6) * 31) + gettime(5); // today's number
	set @income,0;
	// calculate %
	if (#kafrabank < 0) set #kafrabank,0;
	if (#kafrabank == 0) goto L_NoIncomeToday;
	if (#kb_int > @kb_int && #kb_int < @kb_int + 372) set #kb_int,#kb_int - 372; // fix new year (372 = 12 * 31)
	if (#kb_int >= @kb_int) goto L_NoIncomeToday;
	set @income,(#kafrabank / 1000) * (@kb_int - #kb_int); // @income == % of the sum
	// max income constant:
	if (@income > 100000) set @income,100000;

L_NoIncomeToday:
	set #kb_int,@kb_int; // reset days timer

	if (#kafrabank == 0) mes "We could open you an account.";
	if (#kafrabank > 0) mes "Your account: ^135445" + #kafrabank + "^000000 zeny.";
	if (@income > 0) mes "Today's income: ^135445" + @income + "^000000 zeny.";
	set #kafrabank,#kafrabank + @income;
	if (@income > 0) mes "Your new balance: ^135445" + #kafrabank + "^000000 zeny.";
	mes "What would you like?";
	next;
	if (#kafrabank == 0) menu "-Open an account",-,"-Quit",B_EXIT2;
	if (#kafrabank > 0) menu "-Deposit money",-,"-Withdraw money",M_WITHDRAW,"-Quit",B_EXIT2;

	mes"[Maniss]";
	mes "Please, tell me how much zeny you would like to deposit (min.: 1000z).";
	next;
	input @kafrabank;

	if (@kafrabank < 1) goto B_EXIT2;
	if (@kafrabank < 1000) goto L_LESS_1000;
	if (@kafrabank > 10000000) goto L_TOO_BIG_DEPOSIT;
	if (@kafrabank > zeny) goto L_NOT_ENOUGH;
	if (@kafrabank + zeny > 1000000000) goto L_TOO_BIG_BALANCE;
	set zeny,zeny - @kafrabank;
	set #kafrabank, #kafrabank + @kafrabank;
	mes"[Maniss]";
	mes "You've made a deposit of ^135445" + @kafrabank + "z^000000.";
	// we log these zeny operations into the log db
	if (@income > 0) logmes "Balance: " + (#kafrabank - @kafrabank) + "z, Income: " + @income +"z, Deposit: "+ @kafrabank +"z, Final: "+ #kafrabank +"z";
	if (@income <= 0) logmes "Balance: " + (#kafrabank - @kafrabank) + "z, Deposit: " + @kafrabank + "z, Final: " + #kafrabank + "z";
	goto B_EXIT;

L_LESS_1000:
		mes"[Maniss]";
		mes "We're sorry, the minimum amount of zeny you can deposit is 1,000 zeny.";
		next;
		goto B_EXIT2;

L_TOO_BIG_DEPOSIT:
		mes"[Maniss]";
		mes "Sorry. The maximum deposit you can make on a time is 10,000,000 zeny.";
		next;
		goto B_EXIT2;

L_TOO_BIG_BALANCE:
		mes"[Maniss]";
		mes "Sorry. The maximum deposit you can have on your account on a time is 1,000,000,000 zeny.";
		next;
		goto B_EXIT2;

M_WITHDRAW:
	mes"[Maniss]";
	mes "Your account: ^135445" + #kafrabank + "^000000 zeny.";
	mes "How much zeny would you like to withdraw?";
	next;
	input @kafrabank;

	if (@kafrabank < 1) goto B_EXIT2;
	if (@kafrabank > 10000000) goto L_TOO_BIG_WITHDRAW;
	if (@kafrabank > #kafrabank) goto L_NOT_ENOUGH;
	set #kafrabank,#kafrabank - @kafrabank;
	set zeny,zeny + @kafrabank;
	mes"[Maniss]";
	mes "Here is your ^135445" + @kafrabank + "z^000000, put your sign here...";
	// we log these zeny operations into the log db
	if (@income > 0) logmes "Balance: " + (#kafrabank + @kafrabank) + "z, Income: " + @income +"z, Withdraw: "+ @kafrabank +"z, Final: "+ #kafrabank +"z";
	if (@income <= 0) logmes "Balance: " + (#kafrabank + @kafrabank) + "z, Withdraw: " + @kafrabank + "z, Final: " + #kafrabank + "z";
	goto B_EXIT;

L_NOT_ENOUGH:
	mes"[Maniss]";
	mes "You don't have enough zeny for this operation.";
	next;
	goto B_EXIT2;

L_TOO_BIG_WITHDRAW:
	mes"[Maniss]";
	mes "Sorry. The maximum withdraw you can make on a time is 10,000,000 zeny.";
	next;
	goto B_EXIT2;

B_EXIT:
	mes "Your new balance: ^135445" + #kafrabank + "z^000000.";
	mes "Very well... Come again soon!";
	next;

B_EXIT2:
	if (#kafrabank == 0) set #kb_int,0; // reset account_reg #kb_int value
	mes"[Maniss]";
	mes "Thank you for using our Bank Service. We hope to see you again soon.";
	close;
}

geffen.gat,125,73,3	duplicate(bankg)	Bank Clerk#2	833
izlude.gat,145,107,1	duplicate(bankg)	Bank Clerk#3	833
morocc.gat,147,84,7	duplicate(bankg)	Bank Clerk#4	833
payon.gat,170,225,5	duplicate(bankg)	Bank Clerk#5	833
alberta.gat,25,238,5	duplicate(bankg)	Bank Clerk#6	833
aldebaran.gat,135,123,4	duplicate(bankg)	Bank Clerk#7	833
xmas.gat,154,126,9	duplicate(bankg)	Bank Clerk#8	833
comodo.gat,195,151,5	duplicate(bankg)	Bank Clerk#9	833
yuno.gat,339,109,6	duplicate(bankg)	Bank Clerk#10	833
amatsu.gat,102,152,3	duplicate(bankg)	Bank Clerk#11	833
gonryun.gat,157,122,2	duplicate(bankg)	Bank Clerk#12	833
umbala.gat,130,133,6	duplicate(bankg)	Bank Clerk#13	833
louyang.gat,210,102,3	duplicate(bankg)	Bank Clerk#14	833
pay_arche.gat,55,121,3	duplicate(bankg)	Bank Clerk#15	833
ayothaya.gat,212,180,3	duplicate(bankg)	Bank Clerk#16	833
einbroch.gat,57,203,4	duplicate(bankg)	Bank Clerk#17	833
lightalzen.gat,154,105,3	duplicate(bankg)	Bank Clerk#18	833
einbech.gat,181,126,2	duplicate(bankg)	Bank Clerk#19	833
hugel.gat,103,143,7	duplicate(bankg)	Bank Clerk#20	833
