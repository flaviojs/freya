//--------------------------------------------------------------
//               (c)2004-2007 Freya Team Presents:              
//                 ___   ___    ___   _  _   __                 
//                (  _) (  ,)  (  _) ( \/ ) (  )                
//                (  _)  )  \   ) _)  \  /  /__\                
//                (_)   (_)\_) (___) (__/  (_)(_)               
//                    http://www.ro-freya.net                   
//--------------------------------------------------------------
// Script Title: Rentals	Author: kobra_k88/Lupus
//--------------------------------------------------------------
// Revision History: v1.8
//--------------------------------------------------------------
//	1.0 First version [kobra_k88]
//	1.1 Added another Falcon Master into the Hunters Guild [Lupus]
//	1.2 Replaced checkoption(x) with checkriding, checkfalcon [Lupus]
//	1.3 Added support Adv Classes + Baby Class [Lupus]
//	1.4 Added different prices for normal, advanced and baby classes but you could set them to the same [Lupus]
//	1.5 Fixed spelling mistakes [Nexon]
//	1.6 Small fix (Falcon Taming -> Falcon Mastery) [Komurka]
//	1.7 Moved the Falcon Master to Hugel [Poki#3]
//	1.8 Merged in Super Novice Cart Rental
//--------------------------------------------------------------
// Additional Notes: N/A
//--------------------------------------------------------------


// PecoPeco Breeder (For Knights)
prontera.gat,61,338,3	script	PecoPeco Breeder	86,{

	if(Upper==0) set @price,2500; //Normal Peco - default price
	if(Upper==1) set @price,4000; //Armored Peco
	if(Upper==2) set @price,2000; //Baby Peco

	mes "[PecoPeco Breeder]";
	if(BaseJob == Job_Knight || BaseJob == Job_Knight2 ) goto L_Start;
	if(Upper==0)mes "I'm a PecoPeco breeder and I rent out PecoPecos for ^4444FF Knights ^000000 to ride on.";
	if(Upper==1)mes "I'm a PecoPeco breeder and I rent out Armored PecoPecos for ^4444FF Lord Knights ^000000 to ride on.";
	if(Upper==2)mes "I'm a PecoPeco breeder and I rent out Baby PecoPecos for ^4444FF Baby Knights ^000000 to ride on.";
	close;

L_Start:
	if(Upper==0)mes "Good day.  Would you like to ride a PecoPeco?";
	if(Upper==1)mes "Good day.  Would you like to ride an Armored PecoPeco?";
	if(Upper==2)mes "Good day.  Would you like to ride a Baby PecoPeco?";
	next;
	mes "[PecoPeco Breeder]";
	mes "You can rent one PecoPeco for "+@price+" Zeny, provided that you have the PecoPeco Riding skill.";
	next;
	menu "Lend me a trusty steed!... err... bird!",M_0,"Hmm...It's a bit too expensive.",M_1;

	M_0:
		if(getskilllv(63) == 0) goto sL_NeedSkill;
		if(Zeny < @price) goto sL_Zeny;
		if(checkriding(0) != 0) goto sL_GotPeco;
		set Zeny, Zeny - @price;
		setriding;
		mes "Thank you and good day.";
		emotion 15;
		close;

		sL_NeedSkill:
			mes "[PecoPeco Breeder]";
			mes  "As I mentioned earlier, you need the ^3333FFRiding^000000 skill in order to rent a PecoPeco.";
			close;
		sL_Zeny:
			mes "[PecoPeco Breeder]";
			mes "As I said before it will be "+@price+"z. Come back when you have enough zeny.";
			close;
		sL_GotPeco:
			mes "[PecoPeco Breeder]";
			mes "Umm... aren't you already riding a PecoPeco??....";
			emotion 20;
			close;
	M_1:
		mes "[PecoPeco Breeder]";
		mes "Alright.  Good day to you.";
		close;

}

// Grand PecoPeco Breeder (For Crusaders)
prontera.gat,238,312,5	script	Grand PecoPeco Breeder	86,{

	if(Upper==0) set @price,3000; //Normal Peco - default price
	if(Upper==1) set @price,5000; //Armored Peco
	if(Upper==2) set @price,2500; //Baby Peco

	mes "[Grand PecoPeco Breeder]";
	if(BaseJob == Job_Crusader || BaseJob == Job_Crusader2 ) goto L_Start;
	if(Upper==0)mes "I'm a Grand PecoPeco breeder and I rent out Grand PecoPecos for ^4444FF Crusaders^000000 to ride on.";
	if(Upper==1)mes "I'm a Grand PecoPeco breeder and I rent out Armored Grand PecoPecos for ^4444FF Paladins^000000 to ride on.";
	if(Upper==2)mes "I'm a Grand PecoPeco breeder and I rent out Baby Grand PecoPecos for ^4444FF Baby Crusaders^000000 to ride on.";
	close;

L_Start:
	if(Upper==0)mes "Good day.  Would you like to ride a Grand PecoPeco?";
	if(Upper==1)mes "Good day.  Would you like to ride an Armored Grand PecoPeco?";
	if(Upper==2)mes "Good day.  Would you like to ride a Baby Grand PecoPeco?";
	next;
	mes "[Grand PecoPeco Breeder]";
	mes "You can rent one Grand PecoPeco for "+@price+" Zeny, provided that you have the PecoPeco Riding skill.";
	next;
	menu "Lend me a trusty steed!... err... bird!",M_0,"Hmm...It's a bit too expensive.",M_1;

	M_0:
		if(getskilllv(63) == 0) goto sL_NeedSkill;
		if(Zeny < @price) goto sL_Zeny;
		if(checkriding(0) != 0) goto sL_GotPeco;
		set Zeny, Zeny - @price;
		setriding;
		mes "Thank you and good day.";
		emotion 15;
		close;

		sL_NeedSkill:
			mes "[Grand PecoPeco Breeder]";
			mes  "As I mentioned earlier, you need the ^3333FFRiding^000000 skill in order to rent a Grand PecoPeco.";
			close;
		sL_Zeny:
			mes "[Grand PecoPeco Breeder]";
			mes "As I said before it will be "+@price+"z. Come back when you have enough zeny.";
			close;
		sL_GotPeco:
			mes "[Grand PecoPeco Breeder]";
			mes "Umm... aren't you already riding a Grand PecoPeco??....";
			emotion 20;
			close;
	M_1:
		mes "[Grand PecoPeco Breeder]";
		mes "Alright.  Good day to you.";
		close;

}

// Falcon Master (For Hunters)
hu_in01.gat,375,318,5	script	Falcon Master	59,{

	if(Upper==0) set @price,800; //Normal Falcon - default price
	if(Upper==1) set @price,1000; //Scarf Falcon
	if(Upper==2) set @price,600; //Baby Falcon

	mes "[Falcon Master]";
	mes "Hellooooo, I am the Falcon Master.";
	next;
	menu "Speak with him",M_0, "Politely back away",M_End;

	M_0:
		mes "[Falcon Master]";
		if(Upper==0)mes "I can train a Falcon for you, but you need "+@price+"z and be a Hunter with mastered ^5555FF'Falcon Mastery'^000000 skill.";
		if(Upper==1)mes "I can train a Scarf Falcon for you, but you need "+@price+"z and be a Sniper with mastered ^5555FF'Falcon Mastery'^000000 skill.";
		if(Upper==2)mes "I can train a Baby Falcon for you, but you need "+@price+"z and be a Baby Hunter with mastered ^5555FF'Falcon Mastery'^000000 skill.";
		next;
		menu "Ok",sM_0, "No thanks",M_End;

		sM_0:
			if(BaseJob != Job_Hunter) goto sL_NotHunter;
			if(checkfalcon(0) != 0) goto sL_GotFalc;
			if(Zeny < @price) goto sL_Zeny;
			if(getskilllv(127)==0) goto sL_NoSkill;
			set Zeny, Zeny - @price;
			mes "[Falcon Master]";
			mes "It looks like you meet all the requirements to handle a Falcon.  Here you are";
			setfalcon;
			next;
			mes "[Falcon Master]";
			mes "Please take very good care of it.  It will prove to be a valuable companion.";
			close;
			
			sL_NotHunter:
				mes "[Falcon Master]";
				mes "As I said, Falcons can only be given to Hunters.  Falcons are very picky about who they travel with you know.";
				close;
			sL_GotFalc:
				mes "[Falcon Master]";
				mes "You can only have 1 Falcon at a time.";
				close;
			sL_NoSkill:
				mes "[Falcon Master]";
				mes "I'm sorry but you do not have the Falcon Mastery skill.  Please come back when you have learned it.";
				close;
			sL_Zeny:
				mes "[Falcon Master]";
				mes "As I said before it will be "+@price+"z. Come back when you aquire enough zeny.";
				close;
	M_End:
		mes "[Falcon Master]";
		mes "Have a nice day.";
		close;
}


// Super Novice Cart Rental
aldebaran.gat,54,238,5	script	Kafra	117,{

	if (Basejob != Job_Super_Novice) goto L_Otherjob;
	if (checkcart(0) == 1) goto L_GotCart;
	mes "[Kafra]";
	mes "Oh, a Super Novice!";
	mes "You're annoyed that the other Kafras won't lend you a cart?";
	mes "No problem!";
	next;
	mes "[Kafra]";
	mes "I can lend you a cart, but try to keep a low profile,";
	mes "because we are under orders from Kafra management not to lend carts to any Novice.";
	next;
	mes "[Kafra]";
	mes "There will be a fee of ^FF00001900zeny^000000 to use the cart.";
	next;
	mes "[Kafra]";
	mes "You need to have the Push Cart skill in order to use a cart.";
	mes "If you don't have this skill,";
	mes "you won't be able to use the cart and You'll lose your money.";
	mes "Do you want me to lend you a cart?";
	next;
	menu "Lend me a cart",-,"Not Necessary",L_End;
	mes "[Kafra]";
	mes "Since I'm secretly lending you a cart,";
	mes "I can't ensure you have the Push Cart skill.";
	mes "If you don't, you won't be able to use the cart and You'll lose your money.";
	mes "Are you sure you want the cart?";
	next;
	menu "I heard you the first time!",-,"Uhhh...wait a minute...",L_End;
	if (Zeny < 1900) goto L_Error;
	set Zeny,Zeny-1900;
	if (getskilllv(39)==0) goto L_End;
	setcart;
	mes "[Kafra]";
	mes "Thanks for using the Kafra service, even if it is under the table.";
	mes "Hehehehehe...";
	close;

L_Error:
	mes "[Kafra]";
	mes "Hmmm.  It seems you're short of funds.";
	close;

L_End:
	mes "[Kafra]";
	mes "Thank you for your patronage.";
	mes "Please come again.";
	close;

L_GotCart:
	mes "[Kafra]";
	mes "HI There, Dear Super Novice.";
	mes "I'm sorry but I can't help you, you already have an cart!";
	close;

L_Otherjob:
	mes "[Kafra]";
	mes "I'm sorry, but I'm not taking customers right now.";
	mes "Please try asking the other Kafra staff for assistance.";
	close;
}
