//--------------------------------------------------------------
//               (c)2004-2007 Freya Team Presents:              
//                   __                         _               
//                /\ \ \___ _____   _ _ __ ___ (_)              
//               /  \/ / _ \_  / | | | '_ ` _ \| |              
//              / /\  /  __// /| |_| | | | | | | |              
//              \_\ \/ \___/___|\__,_|_| |_| |_|_|              
//                    http://www.ro-freya.net                   
//--------------------------------------------------------------
// Script Title: Rentals NPC	Author: A Lot of People
//--------------------------------------------------------------
// Revision History: v1.4
//--------------------------------------------------------------
//	1.0 First version [Unknown]
//	1.1 Peco Peco option added [Darkchild]
//	1.2 Fixed checkriding/cart/falcon functions [Lupus]
//	1.3 Added support for Baby classes, revised script [Harp]
//	1.4 Made some optimizations and other changes [Tsuyuki]
//--------------------------------------------------------------
// Additional Notes: N/A
//--------------------------------------------------------------


prontera.gat,182,214,3	script	Rentals	906,{

	mes "[Adelia]";
	mes "Hi there! I rent Carts, Falcons, and Pecos.";
	next;

	if (BaseClass == JOB_MERCHANT) goto L_Cart;

	if (BaseClass == JOB_ARCHER) goto L_Falcon;

	if (Basejob == JOB_KNIGHT || Basejob == JOB_KNIGHT2 || Basejob == JOB_CRUSADER || Basejob == JOB_CRUSADER2) goto L_Peco;

	mes "[Adelia]";
	mes "Unfortunately, I can only rent them to certain classes. You don't qualify for my services..";
	close;

// Rent a Cart
L_Cart:
	set @rentitem$, "Cart";
	if (checkcart(0) > 0) goto L_Already_Have;
	set @skillid, 39;
	goto RentItem;

// Rent a Falcon
L_Falcon:
	set @rentitem$, "Falcon";
	if (checkfalcon(0) > 0) goto L_Already_Have;
	set @skillid, 127;
	goto RentItem;

// Rent a Peco
L_Peco:
	set @rentitem$, "Peco";
	if (checkriding(0)>0) goto L_Already_Have;
	set @skillid, 63;
	goto RentItem;

// Checks if user has needed skill and whether the user has a Cart/Falcon/Peco already or not
// If neither is true, then user gets a cart
RentItem:
	if (getskilllv(@skillid)<1) goto L_Need_Skill;
	mes "[Adelia]";	
	mes strcharinfo(0) + ", Would you like to rent a " + @rentitem$ + "?";
	next;
	menu "- Rent a " + @rentitem$,-,"- No thanks",L_Quit;

	if (@rentitem$=="Cart") setcart;
	if (@rentitem$=="Falcon") setfalcon;
	if (@rentitem$=="Peco") setriding;

	L_Quit2:
		mes "[Adelia]";
		mes "Thank you for using my services. Come again!";
		close;

	L_Quit:
		mes "[Adelia]";
		mes "Please come back when you are ready to rent a " + @rentitem$ + "!";
		close;

	L_Need_Skill:
		mes "[Adelia]";
		mes "I'm sorry, but you don't have the required skill to handle a " + @rentitem$ + ".";
		close;

	L_Already_Have:
		mes "[Adelia]";
		mes "Erm.. it seems you already have a ^996633" + @rentitem$ +"^000000.";
		mes "Come back when you actually need my services...";
		close;
}
