//--------------------------------------------------------------
//               (c)2004-2007 Freya Team Presents:              
//                 ___   ___    ___   _  _   __                 
//                (  _) (  ,)  (  _) ( \/ ) (  )                
//                (  _)  )  \   ) _)  \  /  /__\                
//                (_)   (_)\_) (___) (__/  (_)(_)               
//                    http://www.ro-freya.net                   
//--------------------------------------------------------------
// Script Title: Star Gladiator Job Change Quest	Author: Celestria
//--------------------------------------------------------------
// Revision History: v1.2
//--------------------------------------------------------------
//	1.0 First version [Celestria]
//	1.1 Optimized and updated [Lupus]
//	1.2 Improved formatting [Tsuyuki]
//--------------------------------------------------------------
// Additional Notes: Temp quest, highly unofficial.
//--------------------------------------------------------------


payon.gat,215,102,2	script	MooHyun	828,{

	mes "[MooHyun]";
	if (Class == Job_Taekwon && JobLevel >= 40 && STGL_Q == 0) goto L_Taekwon;
	if (STGL_Q > 0) goto L_Quest;
	mes "Aaaah, it's such lovely weather today, don't you think?";
	close;

L_Taekwon:
	mes "Oooh, a Taekwon!";
	mes "I don't see many of those around.";
	mes "I don't suppose you're interested in becoming even stronger?";
	next;
	menu "Oh..?",-,"No, not really.",L_No;

	mes "[MooHyun]";
	mes "Well, there are these dedicated";
	mes "fighters who are known as Star Gladiator.";
	mes "I just so happen to know someone who";
	mes "can warp to their meditation spot.";
	next;
	menu "Oooo.. I wanna be a Star Gladiator.",-,"So?",L_No;
	mes "[MooHyun]";
	mes "What a coincidence!";
	mes "However he does need some rare";
	mes "materials to form the warp.";
	set STGL_Q,1;
	next;
	mes "[MooHyun]";
	goto L_Quest;

L_Quest:
	mes "You will need to bring him:";
	mes "^0000FF- 1 Flame Heart^000000";
	mes "^0000FF- 1 Mystic Frozen^000000";
	mes "^0000FF- 1 Rough Wind^000000";
	mes "^0000FF- 1 Great Nature^000000";
	next;
	mes "[MooHyun]";
	mes "You can find him in Comodo.";
	mes "He normally like to hang out at";
	mes "the top of the mountain there.";
	// mes "Write this down, because I won't tell you again.";
	close;

L_No:
	mes "[MooHyun]";
	mes "I see. Well, if that's how you feel...";
	close;
}

comodo.gat,172,231,2	script	Man#SKTest	730,{

	if (STGL_Q==1) goto L_Talk;
	if (STGL_Q>=2) goto L_Needwarp;
	mes "[Man]";
	mes "...";
	close;

L_Talk:
	mes "[Man]";
	mes "...";
	next;
	mes "[Warper Man]";
	mes "Oh, a Taekwon. I guess MooHyun sent you here then.";
	mes "Did you want me to warp you?";
	next;
	menu "Yes",L_Warp,"No",-;
	mes "[Warper Man]";
	mes "Hmph, wasting my time...";
	mes "Come back when you actually";
	mes "DO want me to warp you.";
	close;

L_Warp:
	if (countitem(994) < 1 || countitem(995) < 1 || countitem(996) < 1 || countitem(997) < 1) goto L_Noitem;
	delitem 994,1;
	delitem 995,1;
	delitem 996,1;
	delitem 997,1;
	set STGL_Q,2;
	mes "[Warper Man]";
	mes "Great! With these items I can create a warp at any time.";
	mes "so if you need to warp again just ask me, okay?";
	next;
	warp "job_star.gat",33,41;
	close;

L_Noitem:
	mes "[Warper Man]";
	mes "In order to warp you, I need rare magical items.";
	next;
	mes "[Warper Man]";
	mes "Bring me:";
	mes "^0000FF- 1 Flame Heart^000000";
	mes "^0000FF- 1 Mystic Frozen^000000";
	mes "^0000FF- 1 Rough Wind^000000";
	mes "^0000FF- 1 Great Nature^000000";
	close;

L_Needwarp:
	mes "[Warper Man]";
	mes "Did you need me to warp you again?";
	next;
	menu "Yes",L_Again,"No",-;
	close;

L_Again:
	warp "job_star.gat",33,41;
	close;
}

job_star.gat,29,33,4	script	Taekwon of the Sun	823,{

	if (STGL_Q == 3) goto L_Warpnow1;
	if (Class == Job_Taekwon && JobLevel >= 40) goto L_Talk1;
	mes "[Taekwon of the Sun]";
	mes "How did you get in here!?";
	mes "Get out at once!";
	next;
	warp "payon.gat",256,242;
	close;

L_Talk1:
	mes "[Taekwon of the Sun]";
	mes "Ah. A Taekwon.";
	mes "It is always good to see a follower of a disciplined art.";
	next;
	mes "[Taekwon of the Sun]";
	mes "I assume you came here to become a Star Gladiator.";
	mes "So allow me to explain...";
	next;
	mes "[Taekwon of the Sun]";
	mes "The Sun, the Moon, and the Stars.";
	mes "All have their own subtle";
	mes "energies. We Star Gladiators use";
	mes "these energies in addition";
	mes "to our own strength to aid us";
	mes "in combat.";
	next;
	mes "[Taekwon of the Sun]";
	mes "In here I teach Taekwons how to";
	mes "tap into the energy of the sun.";
	mes "I shall teach you this now.";
	next;
	mes "^0000FFYou spend several hours meditating^000000";
	mes "^0000FFtogehther under the sun.^000000";
	mes "^0000FFYou do start to feel warm, although^000000";
	mes "^0000FFthat's probably because you aren't^000000";
	mes "^0000FFwearing any suncream.^000000";
	next;
	mes "[Taekwon of the Sun]";
	mes "You have learned all I can teach you.";
	mes "I shall now send you to your";
	mes "next instructor.";
	next;
	warp "job_star.gat",99,41;
	close;

L_Warpnow1:
	warp "job_star.gat",99,41;
	close;
}

job_star.gat,95,34,4	script	Taekwon of the Moon	758,{
	if (STGL_Q == 3) goto L_Warpnow2;
	if (Class == Job_Taekwon && JobLevel >= 40) goto L_Talk2;
	mes "[Taekwon of the Moon]";
	mes "How did you get in here!?";
	mes "Get out at once!";
	next;
	warp "payon.gat",256,242;
	close;

L_Talk2:
	mes "[Taekwon of the Moon]";
	mes "Ah. A Taekwon.";
	mes "It is good that you have";
	mes "managed to come this far.";
	next;
	mes "[Taekwon of the Moon]";
	mes "My job here is to help";
	mes "Taekwons become more in";
	mes "touch with the energies of the Moon.";
	next;
	mes "[Taekwon of the Moon]";
	mes "The Moon has calming energies.";
	mes "So you must be careful not";
	mes "to fall asleep while meditating";
	next;
	mes "[Taekwon of the Moon]";
	mes "Now, sit down.";
	mes "Stare into the Moon and feel.";
	mes "it's light flow through you.";
	next;
	mes "^0000FFAfter a few minutes of meditation^000000";
	mes "^0000FFyour eyes start to grow heavy.^000000";
	mes "^0000FFcomposing yourself you force^000000";
	mes "^0000FFyourself awake, and feel a^000000";
	mes "^0000FFrenewed vigour.^000000";
	next;
	mes "[Taekwon of the Moon]";
	mes "You have learned all I can teach you.";
	mes "I shall now send you to the";
	mes "final instructor.";
	next;
	warp "job_star.gat",165,39;
	close;

L_Warpnow2:
	warp "job_star.gat",165,39;
	close;
}

job_star.gat,161,34,4	script	Taekwon of the Stars	77,{

	if (Class == Job_Taekwon && JobLevel >= 40) goto L_Talk3;
	if (STGL_Q == 3) goto L_Itemcheck;
	mes "[Taekwon of the Stars]";
	mes "How did you get in here!?";
	mes "Get out at once!";
	next;
	warp "payon.gat",256,242;
	close;

L_Talk3:
	mes "[Taekwon of the Stars]";
	mes "Greetings Taekwon.";
	mes "I am here to teach you about";
	mes "the multiple energies that";
	mes "come from the stars.";
	next;

L_Itemcheck:
	if (countitem(1000) < 1 || countitem(1001) < 1) goto L_Noitem3;
	if (SkillPoint > 0) goto L_Skill;
	delitem 1000,1;
	delitem 1001,1;
	mes "[Taekwon of the Stars]";
	mes "Since you have the required items,";
	mes "we shall now begin.";
	next;
	mes "^0000FFAs you meditate you feel a^000000";
	mes "^0000FFpower growing within you.^000000";
	mes "^0000FFThis power does not feel like^000000";
	mes "^0000FFyour own, but like something^000000";
	mes "^0000FFout there is giving you a hand.^000000";
	next;

	callfunc "F_JobChange",Job_Star_Gladiator;
	callfunc "F_ClearJobVariables";

	mes "[Taekwon of the Stars]";
	mes "It seems that you are indeed";
	mes "in touch with the sun, moon, and stars.";
	next;
	mes "[Taekwon of the Stars]";
	mes "I wish you luck on your journey.";
	next;
	warp "payon.gat",256,242;
	close;

L_Skill:
	mes "[Taekwon of the Stars]";
	mes "It would seem you still have";
	mes "some unused skill points.";
	mes "Use these all up, then come";
	mes "talk to me again.";
	close;

L_Noitem3:
	mes "[Taekwon of the Stars]";
	mes "In order to become one with";
	mes "the stars, you will need to bring me:";
	mes "^0000FF- 1 Star Crumb^000000";
	mes "^0000FF- 1 Star Dust^000000";
	next;
	mes "[Taekwon of the Stars]";
	mes "I will now send you back to";
	mes "look for these items.";
	next;
	set STGL_Q,3;
	warp "payon.gat",256,242;
	close;
}
