//--------------------------------------------------------------
//               (c)2004-2007 Freya Team Presents:              
//                   __                         _               
//                /\ \ \___ _____   _ _ __ ___ (_)              
//               /  \/ / _ \_  / | | | '_ ` _ \| |              
//              / /\  /  __// /| |_| | | | | | | |              
//              \_\ \/ \___/___|\__,_|_| |_| |_|_|              
//                    http://www.ro-freya.net                   
//--------------------------------------------------------------
// Script Title: Transcendence Job Change Quest	Author: Nana/Poki/Lupus/Tsuyuki
//--------------------------------------------------------------
// Revision History: v3.7
//--------------------------------------------------------------
//	1.0 Added first version [Nana/Poki]
//	1.1 Made all into functions, checks, quest skills, now Valhallana can return you back [Lupus]
//	 -> She also makes reborn process only when you got no Zeny/items (as in kRO)
//	1.2 Now alternative classes can use their 1-1 job NPC to become Advanced 1st Class [Lupus]
//	1.3 Unknown modifications [eAthena]
//	1.4 Added Baby Class support [Lupus]
//	1.5 Fixed loads of grammatical errors [Mass Zero]
//	1.6 Added a func which prevent Advanced Classes passing 2nd Job Quests again, also guides Adv classes [Lupus]
//	1.7 On reborn reset some Job Quest Vars and Reset Girls Counter [Lupus]
//	1.8 Added Taekwondo classes check [Lupus]
//	1.9 Added support for Job NPC Fase pics, sorrected one dialogue stuff [Lupus]
//	2.0 Removed callfunc Is_####_Class in favor of BaseClass [Silentdragon]
//	2.1 Changed all numbers into constants. [Vicious]
//	2.2 Now ALL quests skills are saved/restored [Lupus]
//	2.3 Removed Duplicates [Silent]
//	2.4 Fixed typos, optimized [Lupus] //= 2.5 Fixed a stuck bug [Lupus]
//	2.6 Adding proper dialogues [Lupus]
//	2.7 Converted to Freya [Tsuyuki]
//	2.8 Rewrote quest skill return process [Tsuyuki]
//	2.9 Redid quest skill saving/returning as functions for other uses [Tsuyuki]
//	 -> Both moved to Global Functions
//	3.0 Fixed a problem where you couldn't exit the Valkyrie Realm under certain circumstances [Tsuyuki]
//	3.1 Fixed problem where High Novice wasn't getting it's quest skills [Tsuyuki]
//	3.2 Finally fixed quest skill saving/returning process [Tsuyuki]
//	3.3 Organized script, and added most of the official dialogue from Aegis [Tsuyuki]
//	3.4 Added official Transporter, converted from Aegis [Tsuyuki]
//	3.5 Novice quest skills are now automatically given for free upon rebirth [Tsuyuki]
//	3.6 Updated The Heart of Ymir's function (now warps to Valkyrie) as per Aegis [Tsuyuki]
//	3.7 You now get a Knife/Clothes when you rebirth, as per officials [Tsuyuki]
//--------------------------------------------------------------
// Additional Notes: * Aegis-certified *
//--------------------------------------------------------------


//--------------------------------------------------------------
// Book of Ymir (Heaven's Door)
//--------------------------------------------------------------

yuno_in02.gat,93,207,1	script	Book of Ymir	111,{

	if (Upper == 1 || valkyrie_Q >= 2) goto L_Ymir01;
	if ((Baselevel >= 99 && Joblevel >= 50 && Upper == 0 && valkyrie_Q > 0) && (Class == JOB_KNIGHT || Class == JOB_PRIEST || Class == JOB_WIZARD || Class == JOB_BLACKSMITH || Class == JOB_HUNTER || Class == JOB_ASSASSIN || Class == JOB_CRUSADER || Class == JOB_MONK || Class == JOB_SAGE || Class == JOB_ALCHEM || Class == JOB_ROGUE || Class == JOB_BARD || Class == JOB_DANCER)) goto L_Ymir02;

	mes "[Book of Ymir]";
	mes "..........................";
	close;

	L_Ymir01:
		mes "[Book of Ymir]";
		mes "...The entrance to the Hall of Fame,";
		mes "has opened to those who have reborn.";
		mes "Those who will walk the path of Heroes, ";
		mes "all the places in this world shall be opened for you.";
		next;
		mes "[Book of Ymir]";
		mes "This place,";
		mes "has prepared for them too.";
		mes "According to the rumors,";
		mes "anything can possibly happen in the Hall.";
		next;
		menu "Stop reading",L_Case01,"Continue",L_Case02;

		L_Case01:
			mes "[Book of Ymir]";
			mes ". . . . .";
			close;

		L_Case02:
			mes "[Book of Ymir]";
			mes "There is a passageway to the temple of Valkyrie";
			mes "at the place nearest to the sky.....";
			mes "The commoners will never know.";
			close2;
			warp "valkyrie.gat",48,8;
			end;

	L_Ymir02:
		mes "[Book of Ymir]";
		mes "...Although the elders have done different great things,";
		mes "there is one matter";
		mes "that none can escape - Death.";
		mes "Even those who lived with the title of Heroes,";
		mes "will meet this moment in their lives.";
		next;
		mes "[Book of Ymir]";
		mes "Alas, none of the records or songs of the Heroes,";
		mes "or even the tales that were passed for generations,";
		mes "is able to tell the final moment of the Heroes.";
		mes "Be acknowledge that this moment,";
		mes "is only stated in the content of Valkyrie";
		mes "in the world of Ragnarok.";
		next;
		mes "[Book of Ymir]";
		mes "If a man has the honorable and greatest qualities,";
		mes "that makes him a figther for the final war,";
		mes "His slain soul shall be leady by the Valkyries";
		mes "to the Hall of Valhalla.";
		mes "His last moment is different from the commoners,";
		mes "for this is the reincarnation of a Hero.";
		next;
		mes "[Book of Ymir]";
		mes "He will be reborn as a real Hero,";
		mes "to shine the people of new era.";
		mes "The fallen body has rised,";
		mes "and the soul shall call upon the new power.";
		mes "Unfortunately, the Heart of Ymir,";
		mes "was lose in this land during the chaotic war.";
		next;
		mes "[Book of Ymir]";
		mes "But this discovery will not be able to support us for the research.";
		mes "Without experiment,";
		mes "we cannot try anything.";
		mes "Therefore, I leave this record,";
		mes "in hope that there'll be someone to find this record";
		mes "together with my will and hope.";
		next;
		mes "[Book of Ymir]";
		mes "Reborn of the Hero shall light the flame of an era.";
		mes "And the long wait shall ends,";
		mes "for the chosen one shall appear,";
		mes "leading the heroes from the hall of Valkyries. And then...";
		mes "...........";
		close;
}


//--------------------------------------------------------------
// Metavis Seith (Librarian)
//--------------------------------------------------------------

yuno_in02.gat,88,164,5	script	Metavis Seith#Library	754,{

	if ((Baselevel >= 99 && Joblevel >= 50 && Upper == 0) && (Class == JOB_KNIGHT || Class == JOB_PRIEST || Class == JOB_WIZARD || Class == JOB_BLACKSMITH || Class == JOB_HUNTER || Class == JOB_ASSASSIN || Class == JOB_CRUSADER || Class == JOB_MONK || Class == JOB_SAGE || Class == JOB_ALCHEM || Class == JOB_ROGUE || Class == JOB_BARD || Class == JOB_DANCER)) goto L_Meta05;
	goto L_Meta06;

	L_Meta05:
		if (valkyrie_Q > 0) goto L_Meta04;
		mes "[Metavis Seith]";
		mes "Welcome, this is the library of";
		mes "Suychervile Magic Academy.";
		mes "Do you come to learn 'The Book of Ymir'?";
		next;
		mes "[Metavis Seith]";
		mes "Although the content of Book of Ymir";
		mes "has been opened to public.";
		mes "Some might want to read it themselves.";
		mes "But since the condition of the book is not that good,";
		mes "we would hope to receive some donation";
		mes "from those who wanted to read it.";
		next;
		mes "[Metavis Seith]";
		mes "The amount of donation should be 1,285,000 zeny";
		mes "This is used for the protection and";
		mes "research of ancient script.";
		mes "If you are willing to pay it, you can enter the venue now.";
		mes "Do you want to pay it?";
		next;
		menu "Yes",L_Meta01,"Cancel",L_Meta02;
			
		L_Meta01:
			if (Zeny < 1285000) goto L_Meta03;
			set Zeny,Zeny-1285000;
			set valkyrie_Q,1;
			mes "[Metavis Seith]";
			mes "Thank you,";
			mes "We will use this well in the research for the academy.";
			mes "You can proceed now.";
			close;

			L_Meta03:
				mes "[Metavis Seith]";
				mes "Looks like you do not have enough zeny.";
				mes "If you want to pay and go in,";
				mes "please make sure that you have enough zeny.";
				close;

		L_Meta02:
			mes "[Metavis Seith]";
			mes "You may proceed.";
			mes "Have a nice visit.";
			close;

	L_Meta04:
		mes "[Metavis Seith]";
		mes "Thank you,";
		mes "We will use this well in the research for the academy.";
		mes "You can proceed now.";
		close;

	L_Meta06:
		mes "[Metavis Seith]";
		mes "Welcome, this is the Suychervile Magic Academy.";
		mes "Enjoy your visit.";
		mes "There are a lot of books here.";
		close;
}


//--------------------------------------------------------------
// The Heart of Ymir
//--------------------------------------------------------------

yuno_in05.gat,49,43,1	script	Heart of Ymir	111,{

	if ((Baselevel >= 99 && Joblevel >= 50 && Upper == 0) && (Class == JOB_KNIGHT || Class == JOB_PRIEST || Class == JOB_WIZARD || Class == JOB_BLACKSMITH || Class == JOB_HUNTER || Class == JOB_ASSASSIN || Class == JOB_CRUSADER || Class == JOB_MONK || Class == JOB_SAGE || Class == JOB_ALCHEM || Class == JOB_ROGUE || Class == JOB_BARD || Class == JOB_DANCER)) goto L_Heart01;
	end;

	L_Heart01:
		if (valkyrie_Q == 1)
			set valkyrie_Q,2;
		if (valkyrie_Q == 2)
			warp "valkyrie.gat",48,8;
		end;
}


//--------------------------------------------------------------
// Researcher (Official??)
//--------------------------------------------------------------

yuno_in02.gat,90,77,4	script	Researcher#06	744,{

	mes "[Researcher]";
	mes "Argh, where is it!?";
	mes "They said that it would be around here somewhere...";
	mes "Maybe I have to look deeper into this castle...";
	emotion e_what;
	close;
}


//--------------------------------------------------------------
// Valkyrie
//--------------------------------------------------------------

valkyrie.gat,48,86,6	script	Valkyrie	811,{

	// Rebirth process for corresponding jobs with proper requirements
	if ((Baselevel >= 99 && Joblevel >= 50 && Upper == 0) && (Class == JOB_KNIGHT || Class == JOB_PRIEST || Class == JOB_WIZARD || Class == JOB_BLACKSMITH || Class == JOB_HUNTER || Class == JOB_ASSASSIN || Class == JOB_CRUSADER || Class == JOB_MONK || Class == JOB_SAGE || Class == JOB_ALCHEM || Class == JOB_ROGUE || Class == JOB_BARD || Class == JOB_DANCER)) goto L_Rebirth;
	if (Class >= JOB_SWORDMAN_HIGH || Class <= JOB_PALADIN2) goto L_Advancing;

	// Normal message
	mes "[Valkyrie]";
	mes "Welcome to the great Valhalla.";
	mes "Alas, it is still not your time.";
	mes "Please leave now and do not corrupt this place of glory.";
	mes "May the honour be with the warriors.!";
	close2;
	warp "yuno_in02.gat",93,205;
	end;

L_Advancing:
	mes "[Valkyrie]";
	mes "Welcome to the great Valhalla.";
	mes "Alas, it is still not your time.";
	mes "Please leave now and do not corrupt this place of glory.";
	mes "May the honour be with the warriors.!";
	close;

L_Rebirth:
	mes "[Valkyrie]";
	mes "Welcome to the great Valhalla.";
	mes "From now onwards, your present life ends here.";
	mes "You shall be given another new path!";
	mes "May the honor be with the warriors!";
	next;

	// No Falcon, Peco, Cart, Zeny, or any items/weight
	if (Zeny > 0 || Weight > 0 || checkcart(0) || checkfalcon(0) || checkriding(0)) goto L_ExtraFail;

	mes "[Valkyrie]";
	mes "You've have cleansed your body and soul.";
	mes "A great will you have there. The honorable spirit of yours will shine once your body and soul are cleansed.";
	next;

	// No leftover skill points
	if (skillpoint != 0) goto L_SkillPointFail;

	mes "[Valkyrie]";
	mes "Good...The memories shall be removed from you.";
	mes "Only the memory of the glorious moment right now shall stay inside of you.";
	next;
	mes "[Valkyrie]";
	mes "Good...Now,";
	mes "One, your pass memory shall be left in Urd.";
	mes "Two, you will only remember you glorious moment in Verdande.";
	mes "Three, You will live your new life in Skuld";
	next;
	mes "[Valkyrie]";
	mes "One";
	next;
	mes "[Valkyrie]";
	mes "Two....";
	next;
	mes "[Valkyrie]";
	mes "Three....";
	next;

	// Memo the target 3rd Job ID
	set ADVJOB, Class+JOB_NOVICE_HIGH;
	if (ADVJOB == JOB_LORD_KNIGHT2) set ADVJOB,JOB_LORD_KNIGHT;
	if (ADVJOB == JOB_PALADIN2) set ADVJOB,JOB_PALADIN;

	// Save Learned Quest Skills
	callfunc "F_SaveQuestSkills";

	// Change to High Novice
	jobchange JOB_NOVICE_HIGH;

	// Reset level down to 1/1
	resetlvl(1);

	// Reset Skills NPC counter
	set RES_SKILL,0;

	// Get free Novice Quest Skills
	skill 142,1,0;
	skill 143,1,0;

	// Clear old job quest variables
	callfunc "F_ClearJobVar";


	mes "[Valkyrie]";
	mes "Good. Congratulations. The process is complete.";
	mes "This is, a gift that I want you to have in your life as a new warrior!";

	// Official Gift Items
	getitem 1201,1; // Knife
	getitem 2301,1; // Cotton Shirts

	next;
	mes "[Valkyrie]";
	mes "May the memory of your life in Urd will not be wasted.";
	mes "May the glorious moment in Verdande be reborn.";
	mes "May the new life in Skuld bring glory again.";
	close2;

	if (ADVJOB == JOB_LORD_KNIGHT || ADVJOB == JOB_PALADIN) goto L_Izlude;
	if (ADVJOB == JOB_HIGH_PRIEST || ADVJOB == JOB_CHAMPION) goto L_Prontera;
	if (ADVJOB == JOB_HIGH_WIZARD || ADVJOB == JOB_PROFESSOR) goto L_Geffen;
	if (ADVJOB == JOB_WHITESMITH || ADVJOB == JOB_CREATOR) goto L_Alberta;
	if (ADVJOB == JOB_SNIPER || ADVJOB == JOB_CLOWN || ADVJOB == JOB_GYPSY) goto L_Payon;
	if (ADVJOB == JOB_ASSASSIN_CROSS || ADVJOB == JOB_STALKER) goto L_Morroc;

L_Prontera:
	savepoint "prontera.gat",273,354;
	warp "prontera.gat",273,354;
	end;

L_Morroc:
	savepoint "morocc.gat",160,94;
	warp "morocc.gat",160,94;
	end;

L_Alberta:
	savepoint "alberta.gat",116,57;
	warp "alberta.gat",116,57;
	end;

L_Payon:
	savepoint "payon.gat",155,90;
	warp "payon.gat",155,90;
	end;

L_Izlude:
	savepoint "izlude.gat",94,103;
	warp "izlude.gat",94,103;
	end;

L_Geffen:
	savepoint "geffen.gat",120,100;
	warp "geffen.gat",120,100;
	end;

L_ExtraFail:
	mes "[Valkyrie]";
	mes "Good..You need to prepare a few items now.";
	mes "First, cleanse your body and soul. The honorable spirit of yours will only be shown if your soul and body are cleansed.";
	next;
	mes "[Valkyrie]";
	mes "All the items and zeny you're carrying has to be cleansed,";
	mes "including the pets, push cart and other stuffs...";
	mes "None can be brought away with you,";
	mes "Now...be prepared and come back later.";
	close2;
	warp "yuno_in02.gat",93,205;
	end;

L_SkillPointFail:
	mes "[Valkyrie]";
	mes "But there seems to be something that you still haven't let go.";
	mes "Release the thing that prevent you from moving on and come back later.";
	close2;
	warp "yuno_in02.gat",93,205;
	end;
}


//--------------------------------------------------------------
// Transporter
//--------------------------------------------------------------

valkyrie.gat,44,33,5	script	Transporter	124,{

	if (Class >= JOB_SWORDMAN_HIGH || Class <= JOB_PALADIN2) goto L_Transport;

	set karma_d,rand(1,10);
	if (karma_d > 4) goto L_Karma01;
	goto L_Karma02;

		L_Karma01:
			mes "[Transporter]";
			mes "Congratulations on the success of your reincarnation.";
			mes "May the honor be with the warriors!!";
			close;

		L_Karma02:
			mes "[Transporter]";
			mes "Please do not touch the display item.";
			mes "..........";
			close;

	L_Transport:
		mes "[Transporter]";
		mes "Please remember your moment of glory.";
		mes "Where do you want to go now?";
		next;
		menu "Prontera",L_Prontera,"Morocc",L_Morroc,"Payon",L_Payon,"Geffen",L_Geffen,"Alberta",L_Alberta,"Izlude",L_Izlude,"Al De Baran",L_Aldebaran,"Comodo",L_Comodo,"Juno",L_Juno;

		L_Prontera:
			mes "[Transporter]";
			mes "Farewell and enjoy your trip then.";
			close2;
			savepoint "prontera.gat",116,72;
			warp "prontera.gat",116,72;
			end;

		L_Morroc:
			mes "[Transporter]";
			mes "Farewell and enjoy your trip then.";
			close2;
			savepoint "morocc.gat",156,46;
			warp "morocc.gat",156,46;
			end;

		L_Payon:
			mes "[Transporter]";
			mes "Farewell and enjoy your trip then.";
			close2;
			savepoint "payon.gat",69,100;
			warp "payon.gat",69,100;
			end;

		L_Geffen:
			mes "[Transporter]";
			mes "Farewell and enjoy your trip then.";
			close2;
			savepoint "geffen.gat",120,39;
			warp "geffen.gat",120,39;
			end;

		L_Alberta:
			mes "[Transporter]";
			mes "Farewell and enjoy your trip then.";
			close2;
			savepoint "alberta.gat",117,56;
			warp "alberta.gat",117,56;
			end;

		L_Izlude:
			mes "[Transporter]";
			mes "Farewell and enjoy your trip then.";
			close2;
			savepoint "izlude.gat",94,103;
			warp "izlude.gat",94,103;
			end;

		L_Aldebaran:
			mes "[Transporter]";
			mes "Farewell and enjoy your trip then.";
			close2;
			savepoint "aldebaran.gat",91,105;
			warp "aldebaran.gat",91,105;
			end;

		L_Comodo:
			mes "[Transporter]";
			mes "Farewell and enjoy your trip then.";
			close2;
			savepoint "comodo.gat",209,143;
			warp "comodo.gat",209,143;
			end;

		L_Juno:
			mes "[Transporter]";
			mes "Farewell and enjoy your trip then.";
			close2;
			savepoint "yuno.gat",328,101;
			warp "yuno.gat",328,101;
			end;
}


//--------------------------------------------------------------
// To High Function
//--------------------------------------------------------------

// function HIGH NOVICE -> HIGH 1
//getarg(0) - High Job ID
//getarg(1) - High Job Name
//getarg(2) - 3rd Job ID
//getarg(3) - current NPC's name

function	script	F_ToHigh	{
	if (Class == JOB_NOVICE) return;
	if (Upper == 2) return; // Baby Class - skip it
	if (ADVJOB == JOB_LORD_KNIGHT2) set ADVJOB,JOB_LORD_KNIGHT;
	if (ADVJOB == JOB_PALADIN2) set ADVJOB,JOB_PALADIN;
	set @fjob,ADVJOB; // Alternative classes should pass, too
	if (@fjob == JOB_STALKER) set @fjob,JOB_ASSASSIN_CROSS;
	if (@fjob == JOB_CREATOR) set @fjob,JOB_WHITESMITH;
	if (@fjob == JOB_CLOWN || @fjob == JOB_GYPSY) set @fjob,JOB_SNIPER;
	if (@fjob == JOB_PALADIN) set @fjob,JOB_LORD_KNIGHT;
	if (@fjob == JOB_CHAMPION) set @fjob,JOB_HIGH_PRIEST;
	if (@fjob == JOB_PROFESSOR) set @fjob,JOB_HIGH_WIZARD;

	if (Class == getarg(0)) goto L_WELCOME; // 3rd Job
	if (Class >= JOB_LORD_KNIGHT && Class <= JOB_PALADIN2) goto L_ALREADY; // Already Advanced Class
	if (Class == JOB_NOVICE_HIGH && @fjob == getarg(2)) goto L_GETHIGH; // High Novice -> High XXXX
	if (Class == JOB_NOVICE_HIGH) goto L_EXTRA;
	return; // This char doesn't want to get high class
		
		L_EXTRA:
		mes "Hello, Novice High! If you are going to became a "+getarg(1)+", then go visit your very first job teacher.";
		close;

L_GETHIGH:
	mes "["+getarg(3)+"]";
	if (JobLevel < 10) goto L_NOTREADY;
	if (checkcart(0)) goto L_CART;
	if (checkfalcon(0)) goto L_FALCON;
	if (checkriding(0)) goto L_PECO;
	if (skillpoint > 0) goto L_SKILLPNTS;

	mes "Hello there, "+strcharinfo(0)+"!";
	mes "You've made a brave choice in coming here to be reborn and stepping forth into the advanced ranks.";
	mes "Now... close your eyes.";
	next;
	mes "["+getarg(3)+"]";
	mes "Open your eyes.";
	mes "You have become a "+getarg(1)+".";
	jobchange getarg(0); // High Class
	next;
	// Return learned quest skills
	callfunc "F_ReturnQuestSkills";
	mes "["+getarg(3)+"]";
	mes "I wish you good fortune in the near future!";
	emotion e_grat;
	close;

L_NOTREADY:
	mes "You are not ready to become a "+getarg(1)+".";
	mes "You have to raise your Job Level to 10.";
	emotion e_gasp;
	close;

L_SKILLPNTS:
	mes "You will need to use up all of your skill points if you want me to continue.";
	emotion e_hmm;
	close;

L_CART:
	mes "Please, drop your cart and we'll continue.";
	emotion e_hmm;
	close;

L_FALCON:
	mes "Please, free your Falcon and we'll continue.";
	emotion e_hmm;
	close;

L_PECO:
	mes "Please, free your Pecopeco and we'll continue.";
	emotion e_hmm;
	close;

L_WELCOME:
	mes "["+getarg(3)+"]";
	mes "You are welcome, "+strcharinfo(0)+"!";
	mes "We're always glad to see a "+getarg(1)+" here!";
	close;

L_ALREADY:
	mes "["+getarg(3)+"]";
	mes "It's such a big honor to salute envoys of Valhalla.";
	mes "Come again.";
	emotion e_ho;
	close;
}


//--------------------------------------------------------------
// Rebirth Function
//--------------------------------------------------------------

// function GET 3rd JOB
//getarg(0) - High Job ID
//getarg(1) - High Job Name
//getarg(2) - 3rd Job ID
//getarg(3) - 3rd Job Name
function	script	F_Rebirth	{
	mes "["+getarg(3)+"]";
	if (Upper == 2) goto L_BABY; // Baby Class - skip it
	if (Class >= JOB_LORD_KNIGHT && Class <= JOB_PALADIN2) goto L_ALREADY; // Already Advanced Class
	if (Class == getarg(0) && ADVJOB == getarg(2)) goto L_GET3RD; //Hight XXXX -> 3rd Job
	mes "Go talk to either Valkyrie or one of my collegues...";
	emotion e_sry;
	close;

L_GET3RD:
	if (JobLevel < 45) goto L_NOTREADY;
	if (checkcart(0)) goto L_CART;
	if (checkfalcon(0)) goto L_FALCON;
	if (checkriding(0)) goto L_PECO;
	if (skillpoint > 0) goto L_SKILLPNTS;

	mes "Congratulations!";
	mes "You have trained well. Now stroll here as a "+getarg(3)+"!";
	jobchange getarg(2); // Rebirth Class
	set ADVJOB,0;
	// Return learned quest skills
	callfunc "F_ReturnQuestSkills";
	close;

L_NOTREADY:
	mes "You have went so far to get here. But I am sorry, you aren't quite ready to become a "+getarg(3)+".";
	mes "You need at least Job Level 45 or higher.";
	emotion e_gasp;
	close;

L_SKILLPNTS:
	mes "You will need to use up all of your skill points if you want me to continue.";
	emotion e_hmm;
	close;

L_CART:
	mes "Please, drop your cart and we'll continue.";
	emotion e_hmm;
	close;

L_FALCON:
	mes "Please, free your Falcon and we'll continue.";
	emotion e_hmm;
	close;

L_PECO:
	mes "Please, free your Pecopeco and we'll continue.";
	emotion e_hmm;
	close;

L_ALREADY:
	mes "Well, hello there! You have been reborn once, there is no second chance.";
	emotion e_ho;
	close;

L_BABY:
	mes "What a lively baby!";
	mes "How did you get here? Go to aunt Valkyrie and ask her to take your home.";
	emotion e_pat;
	close;
}


//--------------------------------------------------------------
// Block High Function
//--------------------------------------------------------------

// Function GUIDE player to Valkyrie for getting 3rd JOB
// This function prevents passing 2nd JOB QUESTS by advanced classes also
//getarg(0) - High Job ID
//getarg(1) - High Job Name
//getarg(2) - 3rd Job ID
//getarg(3) - 3rd Job Name
//getarg(4) - NPC Name
function	script	F_BlockHigh	{
	if (Upper != 1) return; //This func should interact with Advanced classes only
	mes "["+getarg(4)+"]";
	if (Class == getarg(0) && ADVJOB == getarg(2)) goto L_RIGHTHIGH;
	if (Class == getarg(2)) goto L_RIGHT3RD;
	if (Class >= JOB_LORD_KNIGHT && Class <= JOB_PALADIN2) goto L_ALREADY3RD; // Already Advanced Class, but from wrong guild
// This player is a High Novice
	if (Class == JOB_NOVICE_HIGH) mes "Hello, Novice High! If you are going to became a "+getarg(1)+", then go visit your very first job teacher.";
// This player has first Advanced job, but from wrong guild
	if (Class == getarg(0)) mes "A "+getarg(1)+"?";
	if (Class != JOB_NOVICE_HIGH) mes "Rumors say only Valkyrie knows your way...";
	emotion e_sry;
	close;

L_RIGHT3RD:
	mes "Well, hello there! You look... younger.";
	emotion e_hmm;
	next;
	mes "["+getarg(4)+"]";
	mes "You are always welcome here, "+strcharinfo(0)+"!";
	mes "Our good old guild is your second home, isn't it?";
	emotion e_no1;
	close;

L_RIGHTHIGH:
	mes "Hello, "+strcharinfo(0)+"!";
	mes "If you are going to become a "+getarg(3)+", then you should visit Valhalla again.";
	if (JobLevel < 45) mes "But you need at least Job Level 45 or higher.";
	emotion e_gasp;
	close;

L_ALREADY3RD:
	mes "A "+getarg(3)+"?";
	mes "You don't belong to our guild. Begone!";
	emotion e_omg;
	close;
}
